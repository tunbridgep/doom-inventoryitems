class PowerupCommon : CustomInventory
{
    class<Inventory> ReplacementClass;
    String FakePickupMessage;
    int FakeRenderStyle; //this replaces our normal RenderStyle
    int GiveAmount; //for powerups that give more than 1 per pickup (like Soulsphere), give that many when giving out the actual item
    
    //allow skinning
    string Icon1;
    string Icon2;
    property SkinnedIcon: Icon1, Icon2;
    
    meta string ccmd;
    property ConsoleVariable: ccmd;
    
    //holds whether or not we have been added to the last item list
    bool onList;
    
    LastItemUser lastItem;

    Default
    {
        +INVENTORY.PERSISTENTPOWER;
        +INVENTORY.INVBAR;
        +INVENTORY.HUBPOWER;
        Inventory.InterHubAmount 99;
        Inventory.MaxAmount 99;
        Inventory.Amount 1;
        PowerupCommon.SkinnedIcon "", "";
        PowerupCommon.ConsoleVariable "";
    }
    
    override void PostBeginPlay()
    {
        A_SetRenderStyle(Alpha, FakeRenderStyle);
        
        if (CVar.GetCVar(ccmd) == null && inventory_debug)
            Console.Printf("Inventory item "..self.GetTag().." has no cvar set or cvar is incorrect (cvar is "..ccmd.."), and will always be used instantly");
        
        super.PostBeginPlay();
    }
       
    void UseRealItem()
    {
        SimpleInventoryItems handler = SimpleInventoryItems(EventHandler.Find("SimpleInventoryItems"));
        handler.IgnoreNext = true;
        owner.GiveInventory(ReplacementClass,GiveAmount);
        if (onList)
            lastItem.RemoveItem(self);
    }
    
    override String PickupMessage ()
	{
		return FakePickupMessage;
	}
	   
    private void SetIcon()
    {
        if (Icon1 == "" || Icon2 == "")
            return;
    
        TextureID iconID;
    
        switch (inventory_iconset)
        {
            case 0:
               iconID = TexMan.CheckForTexture(Icon1,TexMan.TYPE_ANY,TexMan.DONTCREATE);
               break;
            case 1:
                iconID = TexMan.CheckForTexture(Icon2,TexMan.TYPE_ANY,TexMan.DONTCREATE);
                break;
        }
        if (iconID)
            Icon = iconID;
        else
            Console.Printf("Failed to find icon for item "..self.GetTag());
    }
	
	protected virtual bool GetAutoUse()
	{
        let v = CVar.GetCVar(ccmd);
        return v == null || v.GetBool() == false;  
	}
	
	override void AttachToOwner(Actor other)
    {
        super.AttachToOwner(other);
        if (GetAutoUse())
            owner.UseInventory(self);
        else
        {
            SetIcon();
            lastItem = LastItemUser(other.FindInventory("LastItemUser"));
            lastItem.AddItem(self);
            onList = true;
        }
    }
        
    override bool HandlePickup(Inventory item)
    {
        let handled = super.HandlePickup(item);
        if (handled)
        {
            lastItem.AddItem(self);
            if (GetAutoUse())
                owner.UseInventory(self);
        }
        return handled;
    }
	
	//Used for spawning flares/etc in mods like Brutal Doom, but the game will error out
	//if we try to use a mod where they don't exist (like vanilla), so this does a runtime check
	//instead of a compile time one, and if they exist, spawns them
	protected void SpawnAdditionalItem(String item, int x, int y)
	{       
        Class<Actor> cls = item;
        if (cls)
        {
            A_SpawnItem(item,x,y);
        }
	}
    
    States
    {
        Spawn:
            TNT1 A 0;
            Stop;
        Use:
            TNT1 A 0
            {
                invoker.UseRealItem();
            }
            Stop;
    }
}

class RadsuitReplacer : PowerupCommon
{
    override bool GetAutoUse()
    {
        return super.GetAutoUse() || ShouldInstantlyUseRadsuit();
    }

    private bool ShouldInstantlyUseRadsuit()
    {
        SecSpecial spec;
        owner.cursector.GetSpecial(spec);
        return spec.DamageAmount > 0 && inventory_replace_radsuit > 1 && !owner.CheckInventory("PowerIronFeet",1);
    }
}

class HealthPowerup : PowerupCommon
{   
    //if our health is below the threshold, autouse
    private bool CheckHealth()
    {
        return owner.health <= inventory_checkhealth_amount;
    }
    
    override bool GetAutoUse()
    {
        return super.GetAutoUse() || CheckHealth();
    }
}
