class PowerupCommon : CustomInventory
{
    class<Inventory> ReplacementClass;
    SimpleInventoryItems handler;
    String FakePickupMessage;

    Default
    {
        -COUNTITEM;
        +DONTGIB
        +INVENTORY.ALWAYSPICKUP;
        +INVENTORY.PERSISTENTPOWER;
        +INVENTORY.INVBAR;
        +INVENTORY.HUBPOWER;
        Inventory.InterHubAmount 99;
        Inventory.MaxAmount 99;
        Inventory.Amount 1;
    }

    void UseRealItem()
    {
        handler = SimpleInventoryItems(EventHandler.Find("SimpleInventoryItems"));
        handler.IgnoreNext = true;
        owner.A_GiveInventory(ReplacementClass);
        Destroy();
    }
    
    override String PickupMessage ()
	{
		return FakePickupMessage;
	}
    
    States
    {
        Spawn:
            TNT1 A 0;
            Stop;
        Use:
            TNT1 A 0
            {
                invoker.UseRealItem();
            }
            Stop;
    }
}

class HealthPowerup : PowerupCommon
{
    private bool checked;
    
    //the first frame where we get an owner, check if we are able to be used
    override void Tick()
    {
        if (owner && !checked)
        {
            if (owner.health <= inventory_checkhealth_amount)
                UseRealItem();
            checked = true;
        }
        super.Tick();
    }    
}
