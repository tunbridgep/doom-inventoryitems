class SimpleInventoryItems : EventHandler
{
    Class<Inventory> soulsphere;
    Class<Inventory> blursphere;
    Class<Inventory> megasphere;
    Class<Inventory> berserk;
    Class<Inventory> radsuit;
    Class<Inventory> backpack;
    Class<Inventory> allmap;
    Class<Inventory> infrared;
    Class<Inventory> invulnsphere;
    
    bool IgnoreNext;
    
    override void OnRegister()
    {
        soulsphere = "Soulsphere";
        blursphere = "Blursphere";
        megasphere = "Megasphere";
        berserk = "Berserk";
        radsuit = "Radsuit";
        backpack = "Backpack";
        allmap = "Allmap";
        infrared = "Infrared";
        invulnsphere = "InvulnerabilitySphere";
        IgnoreNext = false;
    }
    
    override void CheckReplacement(ReplaceEvent e)
    {   
        if (!e.replacement)
            return;
            
        Class<Inventory> a = (Class<Inventory>)(e.Replacement);
        
        if (!a)
            return;

        if (e.Replacee is soulsphere) soulsphere = a;
        else if (e.Replacee is blursphere) blursphere = a;
        else if (e.Replacee is megasphere) megasphere = a;
        else if (e.Replacee is invulnsphere) invulnsphere = a;
        else if (e.Replacee is berserk) berserk = a;
        else if (e.Replacee is radsuit) radsuit = a;
        else if (e.Replacee is backpack) backpack = a;
        else if (e.Replacee is allmap) allmap = a;
        else if (e.Replacee is infrared) infrared = a;
    }
    
    //try to emulate an item as closely as possible - have it's pickup sound,
    //it's floatbob, gravity, etc.
    protected void ReplaceCompletely(Inventory source, class<Inventory> replacement_class)
    { 
        if (IgnoreNext)
        {
            IgnoreNext = false;
            return;
        }
        
        if (!source.InStateSequence(source.CurState, source.SpawnState))
        {
            if (inventory_debug)
                console.printf(source.GetTag().." is not in spawn state....not replacing!");
            return;
        }
        
        PowerupCommon replacement = PowerupCommon(Actor.Spawn(replacement_class,source.pos));
        
        if (!replacement)
            return;
        
        replacement.bFLOATBOB = source.bFLOATBOB;
        replacement.bFLOORCLIP = source.bFLOORCLIP;
        replacement.bNOGRAVITY = source.bNOGRAVITY;
        replacement.bVISIBILITYPULSE = source.bVISIBILITYPULSE;
        replacement.bBIGPOWERUP = source.bBIGPOWERUP;
        replacement.bQUIET = source.bQUIET;
        replacement.bISARMOR = source.bISARMOR;
        replacement.bISHEALTH = source.bISHEALTH;
        replacement.bDONTGIB = source.bDONTGIB;
        replacement.bALWAYSPICKUP = source.bALWAYSPICKUP;
        replacement.bBRIGHT = source.bBRIGHT;
        replacement.bFORCEXYBILLBOARD = source.bFORCEXYBILLBOARD;
        replacement.bFORCEYBILLBOARD = source.bFORCEYBILLBOARD;
        replacement.bUNDROPPABLE = source.bUNDROPPABLE;
        replacement.bNEVERRESPAWN = source.bNEVERRESPAWN;
        replacement.bNOSCREENFLASH = source.bNOSCREENFLASH;
        replacement.PickupSound = source.PickupSound;
        replacement.PickupFlash = source.PickupFlash;
        replacement.UseSound = source.PickupSound;
        replacement.FakePickupMessage = source.PickupMsg;
        replacement.GiveAmount = source.Amount;
        replacement.FakeRenderStyle = source.GetRenderStyle();
        replacement.GiveClass = source.GetClassName();
        
        replacement.SetTag(source.GetTag());
        
        //Set TID
        replacement.ChangeTID(source.tid);
        
        //Set up action specials
        if (source.Special != 0)
        {
            replacement.A_SetSpecial(source.Special,source.args[0],source.args[1],source.args[2],source.args[3],source.args[4]);
            source.A_SetSpecial(0,0,0,0,0,0);
        }
        
        //make sure our new item counts and our old one doesn't
        replacement.bCOUNTITEM = source.bCOUNTITEM;
        source.bCOUNTITEM = false;
        
        source.Deactivate(replacement);
        source.Destroy();
    }
    
    //This will give us more compatibility with other mods.
    //See here: https://zdoom.org/wiki/ZScript_classes
    static bool isClass(Actor a, String classname)
    {
        Class<Actor> cls = classname;
        if (cls)
        {
            return a is cls;
        }
        return false;
    }
    
    override void WorldThingSpawned(WorldEvent e)
    {   
        Inventory a = Inventory(e.Thing);
               
        if (!a)
            return;
            
        //Console.printf("Spawned "..a.GetTag());
        
        //else if (a is berserk)  { a.bINVBAR = true; a.bAUTOACTIVATE = false; }
        
        if (a is soulsphere) ReplaceCompletely(a,"SoulspherePickup");
        else if (a is megasphere) ReplaceCompletely(a,"MegaspherePickup");
        else if (a is blursphere) ReplaceCompletely(a,"BlurspherePickup");
        else if (a is invulnsphere) ReplaceCompletely(a,"InvulnerabilitySpherePickup");
        else if (a is berserk) ReplaceCompletely(a,"BerserkPickup");
        
        else if (a is radsuit) ReplaceCompletely(a,"RadsuitPickup");
        else if (a is backpack) ReplaceCompletely(a,"BackpackPickup");
        else if (a is allmap) ReplaceCompletely(a,"AllmapPickup");
        else if (a is infrared) ReplaceCompletely(a,"InfraredPickup");
        
        //PB Items
        else if (isClass(a,"NormalBerserk")) ReplaceCompletely(a,"BerserkPickup");
        else if (isClass(a,"NewAllmap")) ReplaceCompletely(a,"AllmapPickup");
        else if (isClass(a,"MegaBerserk")) ReplaceCompletely(a,"MegaBerserkPickup");
        else if (isClass(a,"NewBlurSphere")) ReplaceCompletely(a,"BlurspherePickup");
        else if (isClass(a,"DoubleSphere")) ReplaceCompletely(a,"DoubleSpherePickup");
        //else if (isClass(a,"RageSphere")) ReplaceCompletely(a,"RageSpherePickup");
        else if (isClass(a,"HasteSphere")) ReplaceCompletely(a,"HasteSpherePickup");
        //else if (isClass(a,"TimeSphere")) ReplaceCompletely(a,"TimeSpherePickup");
        
        else if (isClass(a,"PB_Backpack")) ReplaceCompletely(a,"BackpackPickup");
        else if (isClass(a,"PB_HeavyBackpack")) ReplaceCompletely(a,"CombatBackpackPickup");
        
        else if (isClass(a,"DemonRuneBaron")) ReplaceCompletely(a,"DemonRuneBaronPickup");
        else if (isClass(a,"DemonRuneRevenant")) ReplaceCompletely(a,"DemonRuneRevenantPickup");
    }
}
